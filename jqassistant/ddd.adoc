= Domain-Driven Design

[[ddd:Default]]
[role=group,includesConcepts="ddd:*",includesConstraints="ddd:*"]
== Concepts und Constraints

[[ddd:BoundedContexts]]
.Creates Bounded Context nodes based on the services package.
[source, cypher, role=concept, requiresConcepts="preparation:ShopizerFiles"]
----
MATCH  (p:Package:Shopizer)-[:CONTAINS]->(boundedContext:Package:Shopizer)
WHERE  p.fqn = "com.salesmanager.core.business.services"
WITH   collect(DISTINCT boundedContext.name) AS boundedContexts
UNWIND boundedContexts AS boundedContext
MERGE  (bC:BoundedContext {name: boundedContext, identifier: boundedContext})
RETURN bC.name AS BoundedContext
----

[[ddd:BoundedContextTypes]]
.Links types to their Bounded Contexts.
[source, cypher, role=concept, requiresConcepts="ddd:BoundedContexts"]
----
MATCH  (bC:BoundedContext),
       (p:Package:Shopizer)-[:CONTAINS*]->(t:Type:Shopizer)
WHERE  p.name = bC.name
MERGE  (bC)-[:CONTAINS]->(t)
RETURN bC.name AS  BoundedContext,
       count(DISTINCT t) AS Classes
----

[[ddd:BoundedContextDependencies]]
[plantuml, bc-layer, role=concept, requiresConcepts="ddd:BoundedContextTypes"]
.Aggregates the dependencies between
----
@startuml
!pragma layout smetana

skinparam componentStyle uml2

[{}] as bc1 <<BoundedContext>>
[{}] as type1 <<Type>> <<Shopizer>>
[{}] as bc2 <<BoundedContext>>
[{}] as type2 <<Type>> <<Shopizer>>

bc1 -down-> type1 : :CONTAINS
bc2 -down-> type2 : :CONTAINS
type1 -right-> type2 : :DEPENDS_ON
bc1 -right-> bc2 : (+) :DEPENDS_ON
@enduml
----

[[ddd:ClassOutsideBoundedContext]]
.All types need to be assigned to a bounded context.
[source, cypher, role=constraint, requiresConcepts="ddd:BoundedContextTypes"]
----
MATCH  (t:Type:Shopizer)
WHERE  NOT exists((:BoundedContext)-[:CONTAINS]->(t))
RETURN t.fqn AS TypeWithoutBoundedContext
----
